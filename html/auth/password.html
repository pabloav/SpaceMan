<%attr>
    breadcrumb  => 'Forgot Password'
</%attr>
<%args>
    $id     => ''
    $token  => ''
    $pass1  => ''
    $pass2  => ''
</%args>

<%init>
use SpaceMan::Util qw( generate_action_token validate_action_token );
my $username = '';
my $state = 'request';

if ( $token ) {
    ( my ( $error ), $username ) = validate_action_token( $token );
    if ( $error ) {
        if ( $error eq 'invalid token' ) {
            error( 'Reset link is invalid, please try again.' );
        } elsif ( $error eq 'expired token' ) {
            error( 'Reset link is expired, please try again.' );
        } else {
            error( "Unknown error: $error" );
        }
        $state = 'request';
        $token = '';
        $username = '';
    } else {
        $state = 'passwords';
    }
    if ( $pass1 || $pass2 ) {
        if ( $pass1 ne $pass2 ) {
            error( 'Passwords do not match, please try again.' );
            $pass1 = '';
            $pass2 = '';
        }
        $state = 'passwords';
    }
    if ( $token && $username && $pass1 && $pass2 ) {
        my $user = $SpaceMan->person( $username );
        if ( $user ) {
            $user->set_password( $pass1 );
            $user->update;
            success( 'Password changed!' );
            $state = 'complete';
        } else {
            error( "Could not find an account for '$username'" );
        }
    }
} elsif ( $id ) {
    my $user = $SpaceMan->person( $id );
    if ( $user ) {
        my $token = generate_action_token( 12 * 60, $user->username );
        $user->send_email( 'auth/forgot-password-email.txt' => {
            token   => $token,
        } );
        $state = 'emailed';
    } else {
        error( "Couldn't find an account with that identifier" );
        $state = 'request';
    }
}
</%init>

<div class="row">
    <div class="span6">
        <form method="POST" action="<% $r->uri %>" class="well">

% if ( $token ) {
        <input type="hidden" name="token" value="<% $token %>" />
% }

% if ( $state eq 'request' ) {
    <h2>Password Reset</h2>
    <p>
        Enter your username or email address to have a password-reset link
        emailed to you.
    </p>

    <label for="id">Username or Email Address</label>
    <input class="input-xlarge" name="id" type="text" />
% } elsif ( $state eq 'passwords' ) {
    <h2>Password Reset</h2>
    <p>
        Enter and confirm your new password.
    </p>

    <label for="pass1">Password</label>
    <input class="input-xlarge" name="pass1" type="password" />

    <label for="pass2"> Confirm Password</label>
    <input class="input-xlarge" name="pass2" type="password" />

% } elsif ( $state eq 'emailed' ) {

    <div class="alert-message alert-success">
        <p>
            <strong>You've got password!</strong>
            Check your email for a link to reset your password.
        </p>
    </div>

% } elsif ( $state eq 'complete' ) {

    <div class="alert-message success">
        <p>
            <strong>You've got password!</strong>
            Your password has been successfully changed.
        </p>
    </div>

% }
% unless ( $state eq 'emailed' || $state eq 'complete' ) {
            <div class="actions">
                <button type="submit" class="btn primary">Submit</button>
                &nbsp;
                <button type="reset" class="btn">Cancel</button>
            </div>
% }
        </form>
    </div>
    <div class="span6">
    </div>
</div>
